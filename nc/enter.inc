;----------------------------------------------------------------------------
; MXOS NC.COM
; Клавиша ENTER
;
; 2013-12-18 Дизассемблировано vinxru
;----------------------------------------------------------------------------

Enter:      ; Если ком строка не пустая
            ld      hl, (nc_vars.cmdLineEnd)
            ld      a, nc_vars.cmdLine & 0FFh
            cp      l
            jp nz,  EnterCmdLine

            ; Запомнить текущий диск в регистре b
            ld      e, 0
            call    bios_fileGetSetDrive
            ld      b, a

            ; Подготовить имя файла A:NC.EXT
            ld      hl, aNcExt
            ld      de, nc_vars.tempFileDescr; nc_vars.tempFileDescr используется как буфер
            call    bios_fileNamePrepare
            ex      de, hl

            ; Загрузить файл A:NC.EXT по адресу 0
            ld      hl, nc_vars.tempFileDescr
            ld      de, 0
            ld      c, 1
            call    bios_fileGetSetAddr ; изменить адрес загрузки файла на de = 0
            call    bios_fileLoad
            jp c,   Enter_stdExec

            ; Переключить диск обратно
            ;mov    a, b
            ;mvi    e, 1
            ;call   bios_fileGetSetDrive

            ; Получить имя выбранного файла
            call    getSelectedFile
            ld      a, (hl)
            inc     a
            jp z,   mainLoop ; Ничего не выбрано

            ; hl = указатель на расширение файла
            ld      de, FILE_DESCRIPTOR.ext
            add     hl, de

            ld      de, 0

            ; Сравниваем с расширеним из файла
Enter_extLoop:
            ld      c, 3
            push    hl
Enter_1:
            ld      a, (de)
            cp      (hl)
            jp nz,  Enter_nextExt
            inc     hl
            inc     de
            dec     c
            jp nz,  Enter_1
            pop     hl
            inc     de

            ; Найдено расширение

            ; Копирование имени выбранного файла в ком строку
Enter_5:
            ld      hl, nc_vars.cmdLine
Enter_4:
            ld      a, (de)
            ld      (hl),a
            cp      0Dh
            jp z,   Enter_3
            inc     a
            jp z,   Enter_3
            inc     hl
            inc     de
            jp      Enter_4

; ---------------------------------------------------------------------------

Enter_3:
            ld      (hl), ' '
            inc     hl
            ld      (hl), 0
            ld      (nc_vars.cmdLineEnd), hl

            ; Вывод ком строки на экран
            COLOR   COLOR_CMDSCREEN
            ld      hl, nc_vars.cmdLine
            call    bios_printString

Enter_stdExec:    ; Получить выбранный файл
            call     getSelectedFile

            ; Ничего не выбрано
            ld      a, (hl)
            inc     a
            jp z,   mainLoop

            ; ?
            call    selFileToCmdLine

EnterCmdLine:
            ; Очистка экрана
            COLOR   COLOR_CMDSCREEN
            ld      c, 1Fh
            call    bios_printChar

            ; Помещаем в конец ком строки 0Dh
            ld      hl, (nc_vars.cmdLineEnd)
            ld      (hl), 0Dh

            ; Сохраняем состояние
            call    saveState

            ; Запускаем файл
            ld      hl, nc_vars.cmdLine
            jp      bios_fileExec

