;----------------------------------------------------------------------------
; MXOS NC.COM
; Cохранение состояния в ДОЗУ
;
; 2013-12-18 Дизассемблировано vinxru
;----------------------------------------------------------------------------

saveState:
            ; Устанавливаем побайтовый драйвер
            ld      b, 1
            call    bios_setRAMDDriver

            ld      de, state           ; откуда (в ОЗУ)
            ld      hl, 0FF00h          ; куда (в ДОЗУ)
            ld      b,  stateEnd-state  ; сколько байт

saveStateLoop:
            ld      a, (de)
            ld      c, a                ; записываемый байт в c
            xor     a                   ; 0 страница ДОЗУ
            call    bios_RAMDWrite
            inc     hl
            inc     de
            dec     b
            jp nz,  saveStateLoop

            ; Восстанавливаем блочный драйвер
            ; b уже 0 здесь
            jp      bios_setRAMDDriver


;----------------------------------------------------------------------------
; MXOS NC.COM
; Загрузка состояния из ДОЗУ
;
; выход:
;   флаг  z, если в ДОЗУ есть метка "запускалось"
;   флаг nz, если в ДОЗУ нет  метки "запускалось"
;   Дравер не восстанавливается!
;
; 2022-02-02 SpaceEngineer
;----------------------------------------------------------------------------

loadState:
            ; Устанавливаем побайтовый драйвер
            ld      b, 1
            call    bios_setRAMDDriver

            ; Если в рам-диске по адресу 0FF00h (последний неполный сектор)
            ; нет специальной метки, значит, не запускали форматирование.
            ld      hl, 0FF00h          ; адрес в странице
            xor     a                   ; 0 страница ДОЗУ
            call    bios_RAMDRead
            ld      a, c                ; считанный байт в c
            cp      5Ah
            ret nz                      ; метки нет - выходим с флагом nz

            ; Иначе загружаем состояние
            ld      de, state           ; куда (в ОЗУ)
            ld      hl, 0FF00h          ; откуда (в ДОЗУ)
            ld      b,  stateEnd-state  ; сколько байт байт

loadStateLoop:
            xor     a                   ; 0 страница ДОЗУ
            call    bios_RAMDRead
            ld      a, c                ; считанный байт в c
            ld      (de), a
            inc     hl
            inc     de
            dec     b
            jp nz,  loadStateLoop

            ; Восстанавливаем блочный драйвер
            ; b уже 0 здесь
            call    bios_setRAMDDriver

            xor     a   ; для выхода с флагом z
            ret

