;----------------------------------------------------------------------------
; MXOS
;
; „тение номера кластера из таблицы fat
;
; вход:
;   de = номер €чейки, чтобы прочитать
;
; выход:
;   de = номер кластера, прочитанный из €чейки
;   hl, bc, a - сохран€ютс€
;
; —» аналог:
;   de = fat[de];
;
; 2022-02-04 SpaceEngineer
;----------------------------------------------------------------------------

fatReadCluster:
            push    hl

            ; вычисл€ем адрес €чейки, куда указывает de
            xor     a ; флаг - сектор fat не будет модифицироватьс€
            call    getFatCellAddress

            ; читаем в de номер кластера из fat по адресу hl
            ld      e, (hl)
            inc     hl
            ld      d, (hl)

            ; восстанавливаем регистры и выходим
            pop     hl
            ret

;----------------------------------------------------------------------------
; MXOS
;
; «апись номера кластера в таблицу fat и переход на следующий адрес €чейки
;
; вход:
;   hl = номер €чейки fat, куда писать
;   de = номер кластера, чтобы записать
;
; выход:
;   hl = de
;   bc, a - сохран€ютс€
;
; —» аналог:
;   fat[de] = hl;
;   hl = de;
;
; 2022-02-04 SpaceEngineer
;----------------------------------------------------------------------------

fatWriteCluster:
            ; вычисл€ем адрес €чейки, куда указывает hl
            push    de
            ex      hl, de
            ld      a, 1 ; флаг - сектр fat будет модифицироватьс€
            call    getFatCellAddress
            pop     de

            ; записываем номер кластера из de в fat по адресу hl
            ld      (hl), e
            inc     hl
            ld      (hl), d

            ; hl = de
            ld      h, d
            ld      l, e
            ret

;----------------------------------------------------------------------------
; MXOS
;
; ќсвобождение кластера в таблице fat
;
; вход:
;   de = номер кластера
;
; выход:
;   de = номер следующего кластера
;   hl, bc, a - сохран€ютс€
;
; —» аналог:
;   fat[de] = 0x0000;
;
; 2022-02-04 SpaceEngineer
;----------------------------------------------------------------------------

fatFreeCluster:
            push    hl

            ; вычисл€ем адрес €чейки, куда указывает hl
            push    de
            ex      hl, de
            ld      a, 1 ; флаг - сектор fat будет модифицироватьс€
            call    getFatCellAddress
            pop     de

            ; читаем номер кластера из fat, и сразу записываем туда нули
            ld      e, (hl)
            ld      (hl), 0
            inc     hl
            ld      d, (hl)
            ld      (hl), 0

            ; восстанавливаем регистры и выходим
            pop     hl
            ret


