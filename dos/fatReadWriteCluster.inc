;----------------------------------------------------------------------------
; MXOS
;
; Чтение номера кластера из таблицы fat
;
; вход:
;   de = номер кластера
;
; выход:
;   de = номер следующего кластера
;   остальные регистры сохраняются
;
; СИ аналог:
;   de = fat[de]
;
; 2022-02-04 SpaceEngineer
;----------------------------------------------------------------------------

fatReadCluster:
            push    hl

            ; уможаем de на 2 - смещение элемента с номером кластера в таблице fat
            ex      de, hl
            add     hl, hl

            ; прибавляем к адресу таблицы fat
            ld      de, FAT_BUFFER   ; de = адрес таблицы fat в памяти
            add     hl, de          ; hl = адрес следующего элемента в таблице fat

            ; читаем номер кластера из fat
            ld      e, (hl)
            inc     hl
            ld      d, (hl)

            ; восстанавливаем регистры и выходим
            pop     hl
            ret

;----------------------------------------------------------------------------
; MXOS
;
; Запись номера кластера в таблицу fat и переход на следующий адрес ячейки
;
; вход:
;   hl = адрес ячейки fat, куда писать
;   de = номер кластера, чтобы записать
;
; выход:
;   hl = адрес ячейки fat, куда указывал de
;   остальные регистры сохраняются
;
; СИ аналог:
;   fat[de] = de;
;   hl = &fat[de];
;
; 2022-02-04 SpaceEngineer
;----------------------------------------------------------------------------

fatWriteCluster:
            push    de

            ; записываем номер кластера в fat по адресу hl
            ld      (hl), e
            inc     hl
            ld      (hl), d

            ; уможаем de на 2 - смещение ячейки с номером de в таблице fat
            ex      de, hl
            add     hl, hl

            ; прибавляем к адресу таблицы fat
            ld      de, FAT_BUFFER   ; de = адрес таблицы fat в памяти
            add     hl, de          ; hl = адрес ячеки с номером de в таблице fat

            ; восстанавливаем регистры и выходим
            pop     de
            ret

;----------------------------------------------------------------------------
; MXOS
;
; Освобождение номера кластера в таблице fat
;
; вход:
;   de = номер кластера
;
; выход:
;   de = номер следующего кластера
;   остальные регистры сохраняются
;
; СИ аналог:
;   de = fat[de]
;
; 2022-02-04 SpaceEngineer
;----------------------------------------------------------------------------

fatFreeCluster:
            push    hl

            ; уможаем de на 2 - смещение элемента с номером кластера в таблице fat
            ex      de, hl
            add     hl, hl

            ; прибавляем к адресу таблицы fat
            ld      de, FAT_BUFFER   ; de = адрес таблицы fat в памяти
            add     hl, de          ; hl = адрес следующего элемента в таблице fat

            ; читаем номер кластера из fat, и сразу записываем туда нули
            ld      e, (hl)
            ld      (hl), 0
            inc     hl
            ld      d, (hl)
            ld      (hl), 0

            ; восстанавливаем регистры и выходим
            pop     hl
            ret


