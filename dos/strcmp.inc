;----------------------------------------------------------------------------
; MXOS
; Сравнение строк, чувстсвительное к регистру
;
; вход:
;  de = адрес строки 1
;  hl = адрес строки 2
;  c  = длина строки
;
; выход:
;  zf - строки равны
;
; регистры не сохраняются!
;
; 2022-02-11 SpaceEngineer
;----------------------------------------------------------------------------

strcmp:     ; Сравнение символов
            ld      a, (de)
            cp      (hl)
            ret nz          ; если символы не равны, выходим с флагом nz
            inc     hl
            inc     de

            ; Повтор цикла
            dec     c
            jp nz,  strcmp

            ; Сравнили до конца и не вышли раньше - значит, строки равны
            ret             ; выходим с флагом z

;----------------------------------------------------------------------------
; MXOS
; Сравнение строк, нечувстсвительное к регистру
;
; вход:
;  hl = адрес строки 1
;  de = адрес строки 2
;  c  = длина строки
;
; выход:
;  zf = 1 - строки равны
;
; регистр bc сохраняется
;
; 2022-02-11 SpaceEngineer
;----------------------------------------------------------------------------

stricmp:    push    bc
stricmp_loop:
            ; Читаем символ из первой строки и переводим в верхний регистр
            ld      a, (de)
            call    upperCase
            ld      b, a    ; сохраняем его в b

            ; Читаем символ из второй строки и переводим в верхний регистр
            ld      a, (hl)
            call    upperCase

            ; Сравнение символов
            cp      b
            jp nz,  stcicmp_exit    ; если символы не равны, выходим с флагом zf = 0

            ; Повтор цикла
            inc     hl
            inc     de
            dec     c
            jp nz,  stricmp_loop

            ; Сравнили до конца и не вышли раньше - значит, строки равны
stcicmp_exit:
            pop     bc
            ret             ; выходим с флагом zf = 1

;----------------------------------------------------------------------------
; MXOS
; Преобразование кода символа КОИ-8 в верхний регистр.
;
; вход:
;  a = символ
;
; выход:
;  a = символ в верхнем регистре
;
; все регистры сохраняются
;
; 2022-02-11 SpaceEngineer
;----------------------------------------------------------------------------

upperCase:  ; Цифры, знаки и прописные английские буквы - не меняем
            cp      61h
            ret c

            ; Строчные английские буквы - вычитаем 20h
            cp      7Bh
            jp nc,  upperCase1
            sub     20h
            ret

upperCase1: ; Буква ё - прибавляем 10h
            cp      0A3h
            jp nc,  upperCase2
            add     10h
            ret

upperCase2: ; Символы псевдографики - не меняем
            cp      0C0h
            ret c

            ; Заглавные русские буквы - не маняем
            cp      0E0h
            ret nc

            ; Строчные русские буквы - прибавляем 20h
            add     20h
            ret

;+---------------------------------------------------------------------------
; MXOS
; Сравнение расширений файлов
;
; На входе
;  de - адрес расширения 1
;  v_curFileDescr - имя с расширением 2
;
; На выходе
;  zf = 0 - расширения равны
;  de = de+3
;
; 2013-12-12 Дизассемблировано vinxru
; 2022-02-11 Доработано SpaceEngineer
;----------------------------------------------------------------------------

cmpFileExt:
            ; Адрес расширения
            ld      hl, v_curFileDescr + FILE_NAME_LENGTH
            ld      c, 3   ; cравниваем 3 символа
            call    stricmp ; zf = 1 если равны

            ; Делаем так, чтобы на выходе функции de было
            ; на 3 больше, чем на входе в функцию
            ld      b, 0
            push    af
            ex      de, hl
            add     hl, bc
            ex      de, hl
            pop     af

            ; Выход
            ret

