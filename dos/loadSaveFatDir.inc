;+---------------------------------------------------------------------------
; MXOS
; Сохранить FAT и корневой каталог на устройство
;
; На выходе
;  регистры bc, de, hl сохраняются
;
; 2013-12-12 Дизассемблировано vinxru
;----------------------------------------------------------------------------

saveFatDir: ; Сохраняем регистры
            push    bc
            push    de

            ; Режим - запись
            ld      b, 1

            ; Переход к общему коду             
            jp      loadFatDirCommon

;----------------------------------------------------------------------------
; Загрузить FAT и корневой каталог с устройства
;
; На выходе
;  регистры bc, de, hl сохраняются
;----------------------------------------------------------------------------

loadFatDir: ; Сохраняем регистры
            push    bc
            push    de

            ; Режим - чтение
            ld      b, 2

loadFatDirCommon:
            ; Сохраняем регистры
            push    hl

            ; Чтение/запись буфера fat и каталога в начало диска
            ld      c, FAT_CLUSTERS + DIR_CLUSTERS  ; сколько кластеров читать/писать
            ld      hl, FAT_BUFFER                  ; адрес буфера
            ld      de, 0                           ; первый кластер

loadFatDirLoop:
            call    j_diskDriver
            inc     h       ; следующий блок в памяти, TODO: это работает только для кластеров 256 байт!
            inc     e       ; следующий кластер
            dec     c
            jp nz,  loadFatDirLoop

            ; Восстанавливаем регистры и выходим
            pop     hl
            pop     de
            pop     bc
            ret

