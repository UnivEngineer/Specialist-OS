;+---------------------------------------------------------------------------
; MXOS
; Получение/установка текущего накопителя.
; В режиме 1 функция переключает драйвер накопителя.
;
; На входе 
;   e  - 0=чтение, 1=установка
;   a  - накопитель от 0 до 7
;
; На выходе 
;   a  - накопитель
;   bc, de, hl - сохраняются
;
; 2013-12-12 Дизассемблировано vinxru
; 2022-02-02 Доработано SpaceEngineer
;----------------------------------------------------------------------------

fileGetSetDrive:
            dec     e
            jp z,   fileSelectDrive
            ld      a, (v_drive)
            ret

; ---------------------------------------------------------------------------

fileSelectDrive:
            ; Максимум устройств - 1
            and     7

            ; Если устройство не менялось, выходим
            push    hl
            ld      hl, v_drive
            cp      a, (hl)
            pop     hl
            ret z

            ; Сохраняем выбранное устройство
            ld      (v_drive), a

            ; Пригодится ниже (a = a * 2)
            add     a, a

            ; Сохраняем регистры
            push    hl
            push    de

            ; Получаем точку входа драйвера
            ld      l, a
            ld      h, 0
            ld      de, v_drives
            add     hl, de
            ld      e, (hl)
            inc     hl
            ld      d, (hl)
            ex      de, hl

            ; Сохраняем её
            ld      (j_diskDriver+1), hl

            ; Восстаналвиаем a
            rrca

            ; Очищаем кэш fat
            push    bc
            call    resetSectorCache
            pop     bc

            ; Считываем загрузочный сектор и инициализируем структуру v_diskInfo
            call    fatReadBootSector

            ; Восстанавливаем регистры и выходим
            pop     de
            pop     hl
            ret

