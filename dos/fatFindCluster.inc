;+---------------------------------------------------------------------------
; MXOS
; Поиск свободного кластера 
;
; На выходе
;  cf - ошибка
;  de - номер кластера
;  регистры bc, hl сохраняются
;
; 2013-12-12 Дизассемблировано vinxru
; 2022-02-04 Переработано SpaceEngineer
;----------------------------------------------------------------------------

fatFindClusterFirst:
            ; Сохраняем регистры
            push  hl

            ; de = номер первого кластера для поиска
            ; Первые FAT_CLUSTERS + DIR_CLUSTERS ячеек fat не используются
            ld    de, FAT_CLUSTERS + DIR_CLUSTERS

            jp    fatFindClusterCommon

;----------------------------------------------------------------------------
; Поиск следующего свободного кластера 
;
; На выходе
;  cf - ошибка
;  de - номер кластера
;  регистры bc, hl сохраняются
;----------------------------------------------------------------------------

fatFindClusterNext:
            ; Сохраняем регистры
            push    hl

            ; Предыдущий найденный кластер
            ld      hl, (v_findCluster)

            ; Начинаем поиск со следующего кластера
            inc     hl
            ex      hl, de

            ; Ищем свободный кластер: перебераем все ячейки fat, пока не найдём 0
fatFindClusterCommon:
            push    bc
            ld      bc, FAT_SIZE / FAT_ITEM_SIZE; кол-во ячеек fat

fatFindClusterLoop:
            ; Запоминаем номер последнего кластера в hl
            ld      h, d
            ld      l, e

            ; de = fat[de]
            call    fatReadCluster

            ; Сравниваем de с нулем
            ld      a, d
            or      e
            jp z,   fatFindClusterOk  ; найден

            ; Следующий кластер
            ld      d, h
            ld      e, l
            inc     de

            ; Счётчик просмотренных ячеек fat
            dec     bc
            ld      a, b
            or      c
            jp nz,  fatFindClusterLoop  ; повтор цикла

; ---------------------------------------------------------------------------

            ; Свободный кластер не найден
            ; Восстанавливаем регистры и выходим с a=1, CF=1, ZF=0
            pop     bc
            pop     hl
            ld      a, 1
            scf
            ret

; ---------------------------------------------------------------------------

            ; Свободный кластер найден
fatFindClusterOk:
            ; Устаналиваем ZF=0. Номер кластера не может быть нулевым.
            ; В случае ошибки мы тоже выходим с ZF=1
            or      a

            ; Для ускорения поиска сохраняем номер найденного кластера в v_findCluster
            ld      (v_findCluster), hl
            ex      hl, de

            ; Восстанавливаем регистры и выходим с CF=0, ZF=0
            pop     bc
            pop     hl
            ret

