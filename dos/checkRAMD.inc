;+---------------------------------------------------------------------------
; MXOS
; Исправленная функция определения размера ДОЗУ
; Заимствовано из RAMFOS
;
; На входе
;  а  = максимальный номер страницы, константа MAX_ARAM_PAGE
;  hl = адрес в странице ДОЗУ для записи тестового байта
;
; На выходе
;  bios_vars.maxRamPage = максимальный номер страницы
;  NZ - ДОЗУ найдено (наоборот относительно стандратной функции Ramfos)
;   Z - ДОЗУ не найдено
;
; 2013-11-01 Разработано vinxru
; 2022-02-02 Доработано SpaceEnigneer
;----------------------------------------------------------------------------

checkRAMD:  ; Устанавливаем побайтный драйвер.
            ; Восстанавливать блочный драйвер не надо,
            ; т.к. он будет установлен в reboot3
            ld    b, 1
            call  setRAMDDriver

            ; Записываем во все страницы порядковые номера от большей к меньшей
checkRAMD_0:
            ld      c, a        
            inc     c
            call    bios_RAMDWrite
            dec     a
            jp p,   checkRAMD_0

            ; Читаем номера. Если номер прочитан корректно, значит есть такая страница
            ; a = -1

checkRAMD_1:
            ; Читаем следующую страницу
            inc     a
            call    bios_RAMDRead

             ; Должен быть записан номер страницы+1, если нет, то выходим
            dec     c
            cp      c            
            jp nz,  checkRAMD_2

            ; Сохраняем номер последней страницы RAM-диска
            ld     (bios_vars.maxRamPage), a

            ; Это максимум, выходим
            cp      RAMD_MAX_PAGE          ; RAMD_MAX_PAGE не может быть нулем!
            jp nz,  checkRAMD_1
             
checkRAMD_2:
            ; Если ДОЗУ найдено, выходим c флагом NZ
            or      a
            ret nz

            ; В случае ошибки заносим в v_aramPages=0 и выходим с флагом Z
            ld      (bios_vars.maxRamPage), a
            ret

