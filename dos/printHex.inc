;+---------------------------------------------------------------------------
; MXOS
; Вывод 16-ричного числа на экран (слово)
;
; На входе
;  hl - число
;
; На выходе
;  все регистры сохраняются
;
; 2013-12-12 Дизассемблировано vinxru
;----------------------------------------------------------------------------

printHexWord:     ; Сохраняем регистры
            push  af          

            ; Старший байт
            ld    a, h
            call  printHexByte

            ; Младшйи байт
            ld    a, l
            call  printHexByte

            ; Восстаналиваем регистры
            pop   af
            ret

;----------------------------------------------------------------------------
; Вывод 16-ричного числа на экран (байт)
;
; На входе
;  a - число
;
; На выходе
;  все регистры сохраняются
;----------------------------------------------------------------------------

printHexByte:     ; Сохраняем регистры
            push  bc
            push  af

            ; Сохраняем a для вывода второй цифры
            ld    b, a

            ; Первая цифра - старшие 4 бита
            rlca
            rlca
            rlca
            rlca
            call  printHexNibble

            ; Вторая цифра - младщие 4 бита
            ld    a, b
            call  printHexNibble

            ; Восстаналиваем регистры
            pop   af
            pop   bc
            ret

;----------------------------------------------------------------------------
; Вывод 16-ричного числа на экран (цифра)
;
; На входе
;  a - число, используются младшие 4 бита
;
; На выходе
;  c, a - изменяются, остальные регистры сохраняются
;----------------------------------------------------------------------------

printHexNibble:   and   0Fh
            add   a, '0'
            cp    '9'+1
            jp c, printHexNib_0
              add a, 'A'-'0'-10
printHexNib_0:    ld    c, a
            jp    j_printChar

