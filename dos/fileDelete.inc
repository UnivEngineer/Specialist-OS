;+---------------------------------------------------------------------------
; MXOS
; Удаление файла
;
; На входе
;  hl - имя файла
;
; На выходе
;  cf - ошибка
;  bc, de, hl - сохраняются
;
; 2013-12-12 Дизассемблировано vinxru
;----------------------------------------------------------------------------

fileDelete: ; Найти файл с именем из hl
            call  fileFind
            ret c 

            ; Удалить файл
            call  deleteFileInt

            ; Сохранить изменения на диск
            call  saveFatDir

            ; Результат
            or    a
            ret
            
; ---------------------------------------------------------------------------

deleteFileInt:    ; Сохраняем регистры
            push  hl
            push  bc
            push  de

            ; Помечаем файл, как удаленный заменяя его первую букву на FF
            ld    hl, (v_foundedFile)
            ld  (hl), 0FFh          

            ; Получаем первый кластер файла в a
            ld    de, FILE_DESCRIPTOR.firstCluster
            add   hl, de
            ld  a, (hl)

            ; Адрес таблицы FAT
            ld    h, fat>>8

deleteFileIn_0:   ; Получаем следующий кластер в А
            ld    l, a
            ld  a, (hl)

            ; А этот помечаем не занятым
            ld  (hl), 0

            ; Если это был не последний кластер, продолжаем.
            cp    l
            jp nz,  deleteFileIn_0

            ; Восстаналиваем регистры и выходим
            jp    popDBh_ret

